#
# Copyright (C) 2019-2020 Advanced Micro Devices, Inc. All rights reserved
#

import os
from os import path
from os.path import join as joinpath

Import('env')

#env.Replace(CC='g++',LINKFLAGS=[])

build_root = joinpath(env['BUILDROOT'], 'gtests')

excludes = Glob('*_old.cc')

# Get all Linux files, excluding the list
linux_files = Glob('*linux.cc')

# We first exclude the OS specific files
excludes += linux_files

excluded_list = [f.name for f in excludes]

test_common_srcs = Glob('*.cc', exclude=excluded_list)

plat = env['PLATFORM']
#print("platform is", plat)
if plat == 'linux' or plat == 'posix':
    test_common_srcs += linux_files


PROTOTYPE_GLIBC    = 0xf1
PROTOTYPE_AOCL     = 0xf2
PROTOTYPE_TEST_V1  = 0xf8
PROTOTYPE_TEST_V2  = 0xf9
PROTOTYPE_SVML     = 0xf4

testcflags = ['-funsigned-char', '-fno-strict-aliasing',
                '-fstack-protector-all', '-W', '-Wall',
                '-Wno-multichar' , '-Wno-unused-parameter',
                '-Wno-missing-field-initializers',
                #'-falign-functions=32',
                '-march=native',
                '-std=c++17'
	]

ccflags = []
liblist = []
incpath = ['#gtests/include',
           '#gtests/include/almtest',
           '#gtests/gapi/gtest',
           '#gtests/gapi/gbench',
           '.', './include']
libpath = []

abi = env['libabi']
#print(abi, env['libabi'])

# Default set to AOCL
if abi == 'aocl':
    ccflags = ['-DLIBM_PROTOTYPE=0x%x'%PROTOTYPE_AOCL]
    liblist.extend(['alm'])
elif abi == 'glibc':
    ccflags = ['-DLIBM_PROTOTYPE=0x%x'%PROTOTYPE_GLIBC]
    liblist.extend(['mvec'])
elif abi == 'svml':
    ccflags = ['-DLIBM_PROTOTYPE=0x%x'%PROTOTYPE_SVML]
    liblist.extend(['svml'])
    liblist.extend(['irc'])
    liblist.extend(['intlc'])
else:
    pass

try:
    dbg_mode=env['debug_mode']
except KeyError as err:
    dbg_mode='none'

if dbg_mode in ['gtests', 'all', 'tests' ]:
    env.Append(
        CCFLAGS = ['-ggdb']
       )
    # remove all optimizations on command line
    opt_list=["-O%s"%x for x in range(1, 10)]
    opt_list.append('-Os')
    for opt in opt_list:
        try:
            env['CCFLAGS'].remove(opt)
        except ValueError as err:
            pass
else:
    env.Append(
        CCFLAGS = ['-O0'],
    )

# Add default math library for any unresolved functions
liblist.extend(['m', 'gtest', 'benchmark','mparith32', 'mparith64', 'mpfr'])
#glibpath = ['gapi/gtest', 'gapi/gbench']
glibpath = ['gapi/gtest', 'gapi/gbench','libs/mparith/32', 'libs/mparith/64']
libpath = [joinpath('#', build_root, x) for x in glibpath]

#if intel svml
if abi == 'svml':
    libpath += [env['INTEL_LIB_PATH']]

libtestenv = env.Clone()
libtestenv.Append(
	CFLAGS = ccflags + testcflags,
    CCFLAGS  = ccflags + testcflags,
    CPPPATH  = [incpath],
    VARIANTDIR = 'gtests'
)
libtestenv.Replace(CC='g++',LINKFLAGS=[])

#print([x.path for x in test_common_srcs])
libtest = libtestenv.StaticLibrary('test', test_common_srcs)

e = env.Clone()
e.Replace(CC='g++',LINKFLAGS=[])
e.Append(
	CFLAGS = ccflags + testcflags,
	CCFLAGS = ccflags + testcflags,
	INCPATH = incpath,
	CPPPATH = incpath,
	LIBPATH=libpath,
	LIBS = [libtest] + liblist
)

def getSubdirs(abs_path_dir):
    lst = [ name for name in os.listdir(abs_path_dir) if path.isdir(joinpath(abs_path_dir, name)) and name[0] != '.' ]
    lst.sort()
    return lst

# This sconscript file directory
this_sconscript_file = Dir('.').srcnode().abspath
test_base = os.path.dirname(this_sconscript_file)
gtests = getSubdirs(this_sconscript_file)
#print("tests=================", tests, path.join(test_base, ))
exclude_dirs = ['include', 'gapi', 'libs']
gtests = [x for x in gtests if x not in exclude_dirs]

libg = SConscript('gapi/SConscript',
		exports = {'env': env},
		duplicate = 0,
		src_dir = '#gtests/gapi',
		variant_dir = 'gapi',
	)

libmp = SConscript('libs/mparith/SConscript',
		exports = {'env': env},
		duplicate = 0,
		src_dir = '#gtests/libs/mparith',
		variant_dir = 'libs/mparith',
	)

#print(e.Dump())
#print(gtests)
test_exes = []

for test in gtests:
    tenv = e.Clone()
    tenv.Replace(CC='g++',LINKFLAGS=[])
    tenv.Append(CPPDEFINES = ['-DTEST_NAME=%s' % test])
    exes = SConscript('%s/SConscript' % test,
                        exports = { 'env': tenv },
                        duplicate = 0,
                        src_dir = '#gtests/%s' % test,
                        variant_dir = joinpath(build_root, test)
                        )
    if exes:
       test_exes += exes

Depends(libtest, libmp)
Depends(libtest, libg)
Depends(test_exes, libtest)

Return('libg', 'libmp', 'test_exes', 'libtest')

